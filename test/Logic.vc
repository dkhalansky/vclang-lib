\import Paths
\import Data.Unit
\import Data.Empty
\import Data.Either

\func \infixr 3 && (A B : \Type) => \Sigma A B

\func \infixr 2 || (A B : \Type) => Either A B

\func \infix 4 != {A : \Type} (a b : A) => Not (a = b)

\func \infix 4 <-> (A B : \Type) => (A -> B) && (B -> A)

\func contraposition {A B : \Type} (f : A -> B) (nb : Not B) : Not A =>
    \lam a => nb (f a)

\func contraposition2 {A B : \Type} (f : A -> Not B) (b : B) : Not A =>
    \lam a => f a b

\func Dec (A : \Type) => Not A || A

\func double-neg {A : \Type} (a : A) : Not (Not A) =>
    \lam f => f a

\func cancel-triple-neg  {A : \Type} (nnna : Not (Not (Not A))) : Not A =>
    \lam a => nnna (double-neg a)

\func DNLaw (A : \Type) => Not (Not A) -> A

\func dec-impl-DNLaw (A : \Type) (p : Dec A) : (DNLaw A) => \case p \with {
    | inl f => \lam g => absurd (g f)
    | inr a => \lam _ => a
}
